use std::str::FromStr;
use std::sync::Arc;
use ast::{
    AssignmentData,
    BinaryOpData,
    BinaryOperator,
    CallFunctionData,
    DeclareConstData,
    DeclareFunctionData,
    DeclareRegisterData,
    DeclareVariableData,
    Expression,
    Literal,
    NameData,
    NameType,
    NumberData,
    OrgData,
    ReturnData,
    Type,
};
use src_tag::SrcTag;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, (usize, &'input str), ()>>);

match {
    r"#[^\n]*"
} else {
    _
}

Comment: () = {
    r"#[^\n]*"
};

Number: i32 = {
    // Base 10
    <s:r"[0-9]+"> => i32::from_str(s).unwrap(),
    // Base 16
    <s:r"0x[0-9A-Fa-f]+"> => i32::from_str_radix(&s[2..], 16).unwrap(),
};

Str: Arc<String> = {
    r"\x22(?:[^\x22\\]|\\.)*\x22" => {
        let val = <>;
        let len = val.len();
        Arc::new(String::from_str(&val[1..len-1]).unwrap())
    }
};

Literal: Literal = {
    Number => Literal::Int(<>),
    Str => Literal::Str(<>),
};

Name: Arc<String> = {
    r"[a-zA-Z_][\w]*" => Arc::new(String::from_str(<>).unwrap())
};

Type: Type = {
    "u8" => Type::U8,
    "u16" => Type::U16,
    "void" => Type::Void,
};

NameType: NameType = {
    <n:Name> ":" <t:Type> => NameType { name: n, type_name: t },
};

ParameterList: Vec<NameType> = {
    <l:ParameterList> "," <p:NameType> => {
        let mut result = l;
        result.push(p);
        result
    },
    NameType => vec![<>],
};

ComparisonOp: BinaryOperator = {
    "<" => BinaryOperator::LessThan,
    ">" => BinaryOperator::GreaterThan,
    "<=" => BinaryOperator::LessThanEqual,
    ">=" => BinaryOperator::GreaterThanEqual,
    "==" => BinaryOperator::Equal,
    "!=" => BinaryOperator::NotEqual,
};

Comparison: Box<Expression> = {
    <t:@L> <l:Comparison> <o:ExprOp> <r:Factor> => Box::new(Expression::BinaryOp(BinaryOpData::new(SrcTag(t), o, l, r))),
    Factor,
};

ExprOp: BinaryOperator = {
    "+" => BinaryOperator::Add,
    "-" => BinaryOperator::Sub,
};

Factor: Box<Expression> = {
    <t:@L> <l:Factor> <o:FactorOp> <r:Term> => Box::new(Expression::BinaryOp(BinaryOpData::new(SrcTag(t), o, l, r))),
    Term,
};

FactorOp: BinaryOperator = {
    "*" => BinaryOperator::Mul,
    "/" => BinaryOperator::Div,
};

Term: Box<Expression> = {
    <t:@L> <n:Number> => Box::new(Expression::Number(NumberData::new(SrcTag(t), n))),
    <t:@L> <n:Name> => Box::new(Expression::Name(NameData::new(SrcTag(t), n))),
    FunctionCall,
    "(" <Expression> ")",
};

FunctionCall: Box<Expression> = {
    <t:@L> <n:Name> "(" ")" => Box::new(Expression::CallFunction(CallFunctionData::new(SrcTag(t), n, Vec::new()))),
    <t:@L> <n:Name> "(" <args:ExpressionCommaList> ")" => Box::new(Expression::CallFunction(CallFunctionData::new(SrcTag(t), n, args))),
};

Expression: Box<Expression> = {
    <t:@L> <l:Expression> <o:ComparisonOp> <r:Comparison> => Box::new(Expression::BinaryOp(BinaryOpData::new(SrcTag(t), o, l, r))),
    Comparison,
};

ExpressionCommaList: Vec<Expression> = {
    <l:ExpressionCommaList> "," <e:Expression> => {
        let mut result = l;
        result.push(*e);
        result
    },
    Expression => vec![*<>],
};

Statement: Box<Expression> = {
    Comment => Box::new(Expression::Comment),
    <t:@L> "const" <nt:NameType> "=" <l:Expression> ";" =>
        Box::new(Expression::DeclareConst(DeclareConstData::new(SrcTag(t), nt, l))),
    <t:@L> "var" <nt:NameType> "=" <l:Expression> ";" =>
        Box::new(Expression::DeclareVariable(DeclareVariableData::new(SrcTag(t), nt, l))),
    <t:@L> <n:Name> "=" <l:Expression> ";" =>
        Box::new(Expression::Assignment(AssignmentData::new(SrcTag(t), n, l))),
    "left_shift" <n:Name> ";" => Box::new(Expression::LeftShift(n)),
    "rotate_left" <n:Name> ";" => Box::new(Expression::RotateLeft(n)),
    "rotate_right" <n:Name> ";" => Box::new(Expression::RotateRight(n)),
    "break" ";" => Box::new(Expression::Break),
    <t:@L> "return" <v:Expression> ";" => Box::new(Expression::Return(ReturnData::new(SrcTag(t), Some(v)))),
    <t:@L> "return" ";" => Box::new(Expression::Return(ReturnData::new(SrcTag(t), None))),
    "goto" <n:Name> ";" => Box::new(Expression::GoTo(n)),
    <f:FunctionCall> ";" => f,
    // Error recovery was causing infinite loops
    // See: https://github.com/nikomatsakis/lalrpop/issues/240
    // Can uncomment the following when that's fixed
    //! => {
    //    errors.push(<>);
    //    Box::new(Expression::Error)
    //},
};

StatementList: Vec<Expression> = {
    <l:StatementList> <s:Statement> => {
        let mut result = l;
        result.push(*s);
        result
    },
    Statement => vec![*<>],
};

TopLevelStatement: Box<Expression> = {
    <t:@L> "org" <n:Number> ";" => Box::new(Expression::Org(OrgData::new(SrcTag(t), n))),
    <t:@L> "register" <nt:NameType> "@" <l:Number> ";" =>
        Box::new(Expression::DeclareRegister(DeclareRegisterData::new(SrcTag(t), nt, l))),
    <t:@L> "def" <n:Name> "(" ")" ":" <rt:Type> <b:StatementList> "end" =>
        Box::new(Expression::DeclareFunction(DeclareFunctionData::new(SrcTag(t), n, Vec::new(), rt, b))),
    <t:@L> "def" <n:Name> "(" <pl:ParameterList> ")" ":" <rt:Type> <b:StatementList> "end" =>
        Box::new(Expression::DeclareFunction(DeclareFunctionData::new(SrcTag(t), n, pl, rt, b))),
    Statement,
};

pub Program: Vec<Expression> = {
    <p:Program> <tl:TopLevelStatement> => {
        let mut result = p;
        result.push(*tl);
        result
    },
    TopLevelStatement => vec![*<>],
};
